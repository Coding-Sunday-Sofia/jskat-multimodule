plugins {
	id "com.github.hierynomus.license" version "0.12.1"
	id "com.github.johnrengelman.shadow" version "2.0.2"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.2'
}

apply plugin: 'application'

version = '0.17.0-SNAPSHOT'

allprojects {
	repositories {
	    mavenCentral()
	}

	apply plugin: 'eclipse'
	apply plugin: 'idea'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'jacoco'
	apply plugin: 'license'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	task sourcesJar(type: Jar, dependsOn: classes) {
	    classifier = 'sources'
	    from sourceSets.main.allSource
	}

	task javadocJar(type: Jar, dependsOn: javadoc) {
	    classifier = 'javadoc'
	    from javadoc.destinationDir
	}

	artifacts {
	    archives sourcesJar
	    archives javadocJar
	}
	
	jacocoTestReport {
	    reports {
    	    xml.enabled = true
        	html.enabled = true
    	}
    }

	check.dependsOn jacocoTestReport
	
	license {
    	include "**/*.java"
    	header project.file('LICENSE.header')
    	ext.year = Calendar.getInstance().get(Calendar.YEAR)
    }
    
//    javadoc {
//    	options.addBooleanOption('html5', true)
//	}
	
	test {
	    testLogging {
	        events "passed", "skipped", "failed"
	        exceptionFormat "full"
	    }
	}
}

dependencies {
	compile project(':jskat-base')
	compile project(':jskat-swing-gui')
	compile project(':jskat-javafx-gui')
}

startScripts {
	mainClassName 'org.jskat.JSkatFX'
}

shadowJar {
	baseName = 'JSkat'
	mainClassName = 'org.jskat.JSkatFX'
}