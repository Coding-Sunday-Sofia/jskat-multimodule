buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.google.gradle:osdetector-gradle-plugin:1.6.0'
	}
}

plugins {
	id "com.github.hierynomus.license" version "0.12.1"
	id "com.github.johnrengelman.shadow" version "2.0.2"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.2'
}

apply plugin: 'application'
apply plugin: 'com.google.osdetector'

ext.platform = osdetector.os == 'osx' ? 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os

version = '0.17.0-SNAPSHOT'

allprojects {
	repositories {
	    mavenCentral()
	}

	apply plugin: 'eclipse'
	apply plugin: 'idea'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'license'

	sourceCompatibility = 11
	targetCompatibility = 11
	
	task sourcesJar(type: Jar, dependsOn: classes) {
	    classifier = 'sources'
	    from sourceSets.main.allSource
	}

	task javadocJar(type: Jar, dependsOn: javadoc) {
	    classifier = 'javadoc'
	    from javadoc.destinationDir
	}

	artifacts {
	    archives sourcesJar
	    archives javadocJar
	}
	
	license {
    	include "**/*.java"
    	header project.file('LICENSE.header')
    	ext.year = Calendar.getInstance().get(Calendar.YEAR)
    }
    
    javadoc {
    	options.addBooleanOption('html5', true)
	}
	
	test {
	    testLogging {
	        events "passed", "skipped", "failed"
	        exceptionFormat "full"
	    }
	}
}

dependencies {
	compile project(':jskat-base')
	compile project(':jskat-swing-gui')
	compile project(':jskat-javafx-gui')
}

startScripts {
	mainClassName 'org.jskat.JSkatFX'
}

shadowJar {
	baseName = 'JSkat'
	mainClassName = 'org.jskat.JSkatFX'
}

run {
	doFirst {
		jvmArgs = [
				'--module-path', classpath.asPath,
				'--add-modules', 'javafx.controls'
		]
	}
}