buildscript {
	repositories {
		mavenCentral()
	}
}

plugins {
	id "com.github.hierynomus.license" version "0.12.1"
	id "com.github.johnrengelman.shadow" version "2.0.2"
	id "org.openjfx.javafxplugin" version "0.0.5"
}

task wrapper(type: Wrapper) {
    gradleVersion = '5.0'
}

apply plugin: 'application'

version = '0.17.0-SNAPSHOT'

allprojects {
	repositories {
	    mavenCentral()
	}

	apply plugin: 'eclipse'
	apply plugin: 'idea'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'license'

	sourceCompatibility = 11
	targetCompatibility = 11
	
	task sourcesJar(type: Jar, dependsOn: classes) {
	    classifier = 'sources'
	    from sourceSets.main.allSource
	}

	task javadocJar(type: Jar, dependsOn: javadoc) {
	    classifier = 'javadoc'
	    from javadoc.destinationDir
	}

	artifacts {
	    archives sourcesJar
	    archives javadocJar
	}
	
	license {
    	include "**/*.java"
    	header project.file('LICENSE.header')
    	ext.year = Calendar.getInstance().get(Calendar.YEAR)
    }
    
    javadoc {
    	options.addBooleanOption('html5', true)
	}
	
	test {
	    testLogging {
	        events "passed", "skipped", "failed"
	        exceptionFormat "full"
	    }
	}
}

dependencies {
	compile project(':jskat-base')
	compile project(':jskat-swing-gui')
	compile project(':jskat-javafx-gui')
}

javafx {
	modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.web', 'javafx.swing' ]
}

startScripts {
	mainClassName 'org.jskat.JSkatFX'
	applicationDefaultJvmArgs = ["-Djdk.gtk.version=2"]
}

shadowJar {
	baseName = 'JSkat'
	mainClassName = 'org.jskat.JSkatFX'
}