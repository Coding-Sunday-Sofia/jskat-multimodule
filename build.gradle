plugins {
	id "com.github.johnrengelman.shadow" version "1.2.3"
	id "com.github.hierynomus.license" version "0.12.1"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

allprojects {
	repositories {
	    mavenCentral()
	}
}

allprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'jacoco'
	apply plugin: 'license'

	task sourcesJar(type: Jar, dependsOn: classes) {
	    classifier = 'sources'
	    from sourceSets.main.allSource
	}

	task javadocJar(type: Jar, dependsOn: javadoc) {
	    classifier = 'javadoc'
	    from javadoc.destinationDir
	}

	artifacts {
	    archives sourcesJar
	    archives javadocJar
	}
	
	jacocoTestReport {
	    reports {
    	    xml.enabled = true
        	html.enabled = true
    	}
    }

	check.dependsOn jacocoTestReport
	
	license {
    	include "**/*.java"
    	header project.file('LICENSE.header')
    	ext.year = Calendar.getInstance().get(Calendar.YEAR)
    }
}

dependencies {
	compile project(':jskat-base')
	compile project(':jskat-swing-gui')
	compile project(':jskat-javafx-gui')
}

version = '0.16.0'

jar {
    manifest {
        attributes 'Implementation-Title': 'JSkat',
                   'Implementation-Version': version,
                   'Main-Class': 'org.jskat.JSkatFX'
    }
}

shadowJar {
	baseName = 'JSkat'
	classifier = ''
}